# Import all functions
#---------------------------------------------------------------------------------------------------
{ lib, pkgs, ... }:

(import ./network.nix { inherit lib pkgs;}) // 
(import ./service.nix { inherit lib pkgs;}) // 

# Simple functions
#---------------------------------------------------------------------------------------------------
{
  # Convert a bool into an int
  boolToInt = x: if x then 1 else 0;

  # Convert a bool into a string
  boolToStr = x: if x then "true" else "false";

  # Convert a bool into an integer then to a string
  boolToIntStr = x: if x then "1" else "0";

  # Convert the given json file into nix attribute set
  #-------------------------------------------------------------------------------------------------
  # Usage:
  # local_args = f.fromJSON ./args.dec.json;
  fromJSON = jsonFile:
    builtins.fromJSON (builtins.readFile jsonFile);

  # Convert the given yaml file into nix attribute set
  #-------------------------------------------------------------------------------------------------
  # Usage:
  # local_args = f.fromYAML ./args.dec.yaml;
  fromYAML = yamlFile:
    let
      json = pkgs.runCommand "converted.json" { } ''
        ${lib.getExe pkgs.yj} < ${yamlFile} > $out
      '';
    in builtins.fromJSON (builtins.readFile json);

  # Provide mkIf support for an else clause
  #-------------------------------------------------------------------------------------------------
  # Usage: 
  #   config.xdg.configFile = (f.mkIfElse cfg.vesktop.enable
  #     { "vesktop/themes".source = catppuccinThemesSrc; }    
  #     { "vencord/themes".source = catppuccinThemesSrc; }    
  #   );
  mkIfElse = p: yes: no: lib.mkMerge [
    (lib.mkIf p yes)
    (lib.mkIf (!p) no)
  ];

  # Extract the specific secret by name
  # - secrets: is the list of secrets
  # - name: the target secret's name to key off from
  # - returns secret's value
  #-------------------------------------------------------------------------------------------------
  getSecret = secrets: name: let
    secret = lib.findFirst(x: x.name == name) null secrets;
  in
    if secret == null then
      throw "No secret with name \"${name}\" found"
    else secret.value;
}
